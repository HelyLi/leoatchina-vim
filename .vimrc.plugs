" This is leoatchina's vim config forked from https://github.com/spf13/spf13-vim
" Sincerely thank him for his great job, and I have made some change according to own requires.
"
"                    __ _ _____              _
"         ___ _ __  / _/ |___ /      __   __(_)_ __ ___
"        / __| '_ \| |_| | |_ \ _____\ \ / /| | '_ ` _ \
"        \__ \ |_) |  _| |___) |_____|\ V / | | | | | | |
"        |___/ .__/|_| |_|____/        \_/  |_|_| |_| |_|
"            |_|
" You can find spf13's origin config  at http://spf13.com
"
" plug install path
if has('nvim')
    let $PLUG_PATH = '~/.nvim/plug'
else
    if has("gui_running")
        let $PLUG_PATH = '~/.gvim/plug'
    else
        let $PLUG_PATH = '~/.vim/plug'
    endif
endif
set rtp+=$PLUG_PATH
" complete enviroment
let g:complete_method = "None"
let g:complete_snippet = "None"
" python support
if has('python3') && executable('python3')
    let g:python_version = 3
	if !WINDOWS()
		silent! python3 1
	endif
elseif has('python') && executable('python')
    let g:python_version = 2
	if !WINDOWS()
		silent! python 1
	endif
else
    let g:python_version = 0
endif
" vim_advance judge
if has('nvim') || v:version > 799
    let g:vim_advance = 2
elseif v:version > 703
    let g:vim_advance = 1
else
    let g:vim_advance = 0
endif

" plugins
call plug#begin($PLUG_PATH)
" Use local config, some tested feature can be add to it
if filereadable(expand("~/.nvimrc.local")) && has('nvim')
    source ~/.nvimrc.local
elseif filereadable(expand("~/.gvimrc.local")) && has('gui_running')
    source ~/.gvimrc.local
elseif filereadable(expand("~/.vimrc.local"))
    source ~/.vimrc.local
endif
if WINDOWS()
    Plug 'leoatchina/vim-windows-tools', {'dir':'$HOME\.vim-windows-tools'}
    let $PATH.=";".$HOME."\\.vim-windows-tools"
endif
if (has('nvim') && WINDOWS()) || (has('gui_running') && !WINDOWS())
    Plug 'lambdalisue/vim-fullscreen'
endif
Plug 'mhinz/vim-startify'
Plug 'kshenoy/vim-signature'
Plug 'google/vim-searchindex'
Plug 'scrooloose/nerdcommenter'
Plug 'tpope/vim-commentary'
Plug 'mbbill/undotree'
Plug 'scrooloose/nerdtree'
Plug 'jistr/vim-nerdtree-tabs'
Plug 'easymotion/vim-easymotion'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-surround'
Plug 'leoatchina/vim-colorschemes-collections'
if g:python_version > 0
    Plug 'vim-voom/VOoM'
endif
if executable('git')
    Plug 'tpope/vim-fugitive'
    Plug 'Xuyuanp/nerdtree-git-plugin'
endif
if executable('ctags')
    Plug 'majutsushi/tagbar'
endif
" In your .vimrc.local file
" list only the plugin groups you will use
if !exists('g:plug_groups')
    let g:plug_groups=['smartcomplete', 'php', 'javascript', 'html', 'python']
endif
" input method
if count(g:plug_groups, 'pinyin') || count(g:plug_groups, 'wubi')
    Plug 'leoatchina/ywvim'
endif
" biosyntax
if count(g:plug_groups, 'bio')
    Plug 'bioSyntax/bioSyntax-vim'
endif
" external search method
if g:vim_advance == 2
    Plug 'wsdjeg/FlyGrep.vim'
    nmap <C-f>f :FlyGrep<Cr>
endif
if executable('ag') || executable('ack') || executable('rg') || executable('pt')
    Plug 'dyng/ctrlsf.vim'
    nmap <C-F>s <Plug>CtrlSFPrompt
    nmap <C-F>c <Plug>CtrlSFCwordPath
    nmap <C-F>p <Plug>CtrlSFPwordPath
    nmap <C-F>o :CtrlSFOpen<CR>
    nmap <C-F>t :CtrlSFToggle<CR>
    " vmap
    vmap <C-F>s <Plug>CtrlSFVwordExec
    vmap <C-F>f <Plug>CtrlSFVwordPath
    let g:ctrlsf_position='right'
else
    if v:version >799 && !WINDOWS() && executable('ctags')
        Plug 'damage220/vim-finder'
        nnoremap <C-f>s :finder<Tab>
    else
        nnoremap <C-f>s :vimgrep<Space>
    endif
endif
Plug 'brooth/far.vim', {'do': 'UpdateRemotePlugins'}
Plug 'terryma/vim-multiple-cursors'
" signs
Plug 'gorodinskiy/vim-coloresque'
Plug 'rhysd/conflict-marker.vim'
Plug 'jiangmiao/auto-pairs'
Plug 'nathanaelkane/vim-indent-guides'
Plug 'mhinz/vim-signify'
Plug 'tamlok/vim-markdown'
Plug 'junegunn/vim-easy-align'
" statusline
Plug 'bling/vim-bufferline'
if count(g:plug_groups, 'airline')
    Plug 'vim-airline/vim-airline'
elseif has('statusline')
    Plug 'itchyny/lightline.vim'
    if g:vim_advance == 2 && count(g:plug_groups, 'syntax')
        Plug 'maximbaz/lightline-ale'
    endi
endif
" shell
if count(g:plug_groups, 'shell') && !WINDOWS()
    Plug 'shougo/vimproc.vim', {'do':'make'}
    Plug 'shougo/vimshell.vim'
endif
" PHP
if count(g:plug_groups, 'php')
    Plug 'arnaud-lb/vim-php-namespace', {'for' : 'php'}
endif
" Python
if count(g:plug_groups, 'python')
    Plug 'python-mode/python-mode', { 'for': 'python', 'branch': 'develop' }
endif
" rust
if count(g:plug_groups, 'rust')
    Plug 'rust-lang/rust.vim'
    Plug 'timonv/vim-cargo'
endif
" Javascript
if count(g:plug_groups, 'javascript')
    Plug 'elzr/vim-json', {'for': 'javascript'}
    Plug 'pangloss/vim-javascript', {'for': 'javascript'}
    Plug 'heavenshell/vim-jsdoc', {'for': 'javascript'}
endif
" Go
if count(g:plug_groups, 'go')
    Plug 'fatih/vim-go', {'for': 'go', 'do':':GoUpdateBinaries'}
    Plug 'garyburd/go-explorer', {'for': 'go'}
    if has('nvim')
        Plug 'jodosha/vim-godebug', {'for': 'go'}
    endif
endif
" HTML
if count(g:plug_groups, 'html')
    Plug 'hail2u/vim-css3-syntax', {'for':['css','htm', 'html', 'xml', 'css3']}
    Plug 'tpope/vim-haml'
    Plug 'mattn/emmet-vim'
endif
" browser tools
let g:ctrlp_version = 1
if count(g:plug_groups, 'fzf')
    if !WINDOWS()
        let g:ctrlp_version = 4
    endif
elseif count(g:plug_groups, 'denite')
    if g:python_version == 3 && g:vim_advance == 2
        let g:ctrlp_version = 3
    endif
elseif count(g:plug_groups, 'leaderF')
    if g:python_version && executable('gcc') && g:vim_advance
        let g:ctrlp_version = 2
    endif
endif
if g:ctrlp_version == 4
    Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all' }
    Plug 'junegunn/fzf.vim'
elseif g:ctrlp_version == 3
    Plug 'shougo/denite.nvim'
elseif g:ctrlp_version == 2
    if WINDOWS()
        Plug 'Yggdroot/LeaderF', { 'do': '.\install.bat' }
    else
        Plug 'Yggdroot/LeaderF', { 'do': './install.sh' }
    endif
else
    Plug 'ctrlpvim/ctrlp.vim'
    Plug 'tacahiroy/ctrlp-funky'
endif
" complete
if g:vim_advance
    " vim version
    if g:vim_advance == 2
        if count(g:plug_groups, 'syntax')
            Plug 'w0rp/ale'
        endif
        Plug 'skywind3000/asyncrun.vim'
        if executable('ctags')
            Plug 'ludovicchabant/vim-gutentags'
        endif
    else
        " install run and syntax tools
        if count(g:plug_groups, 'syntax')
            Plug 'vim-syntastic/syntastic'
        endif
    endif
    Plug 'thinca/vim-quickrun'
    " select a complete plugin
    if count(g:plug_groups, 'smartcomplete')
        if g:python_version == 3 && has('nvim') && has('timers')
            if WINDOWS()
                let g:complete_method = "deoplete"
            else
                let g:complete_method = "ncm2"
            endif
        elseif g:python_version  && !has('nvim') && version > 799
            let g:complete_method = "completor"
        elseif has('lua')
            let g:complete_method = "neocomplete"
        else
            let g:complete_method = "neocomplcache"
        endif
    elseif count(g:plug_groups, 'deoplete')
        if g:python_version == 3 && g:vim_advance == 2 && has('timers')
            let g:complete_method = "deoplete"
        else
            echoe "Cannot install deoplete, install neocomplcache instead."
            let g:complete_method = "neocomplcache"
        endif
    elseif count(g:plug_groups, 'completor')
        if g:python_version && !has('nvim') && version > 799
            let g:complete_method = "completor"
        else
            echoe "Cannot install completor, install neocomplcache instead."
            let g:complete_method = "neocomplcache"
        endif
    elseif count(g:plug_groups, 'neocomplete')
        if has('lua')
            let g:complete_method = "neocomplete"
        else
            echoe "Cannot install neocomplete, install neocomplcache instead."
            let g:complete_method = "neocomplcache"
        endif
    elseif count(g:plug_groups, 'neocomplcache')
        let g:complete_method = "neocomplcache"
    elseif count(g:plug_groups, 'youcompleteme') || count(g:plug_groups, 'YCM')
        if executable('cmake') && g:python_version
            let g:complete_method = "YCM"
        else
            echoe "Cannot install YouCompleteMe, install neocomplcache instead."
            let g:complete_method = "neocomplcache"
        endif
    elseif count(g:plug_groups, 'ncm2')
        if g:python_version == 3 && g:vim_advance == 2
            let g:complete_method = "ncm2"
        else
            echoe "Cannot install ncm2, install neocomplcache instead."
            let g:complete_method = "neocomplcache"
        endif
    endif
    " install snippets plugins
    if g:complete_method != "None"
        if count(g:plug_groups,'ultisnips')
            " force ultisnips , but sometime not work
            let g:complete_snippet = "ultisnips"
            Plug 'SirVer/ultisnips'
            Plug 'honza/vim-snippets'
            Plug 'leoatchina/leoatchina-snippets'
        elseif count(g:plug_groups,'neosnippet')
            let g:complete_snippet = "neosnippet"
            Plug 'Shougo/neosnippet'
            Plug 'Shougo/neosnippet-snippets'
        elseif g:python_version && (!has('nvim') || has('nvim') && !WINDOWS())
            let g:complete_snippet = "ultisnips"
            Plug 'SirVer/ultisnips'
            Plug 'honza/vim-snippets'
            Plug 'leoatchina/leoatchina-snippets'
        else
            let g:complete_snippet = "neosnippet"
            Plug 'Shougo/neosnippet'
            Plug 'Shougo/neosnippet-snippets'
        endif
    endif
    " install complete plugins
    if g:complete_method == "deoplete"
        if has('nvim')
            Plug 'Shougo/deoplete.nvim',  { 'do': ':UpdateRemotePlugins'}
        else
            Plug 'Shougo/deoplete.nvim'
            Plug 'roxma/vim-hug-neovim-rpc'
            Plug 'roxma/nvim-yarp'
        endif
        Plug 'Shougo/neoinclude.vim'
        Plug 'zchee/deoplete-clang', {'for': ['c', 'cpp']}
        if count(g:plug_groups, 'python')
            Plug 'zchee/deoplete-jedi', {'for': 'python'}
        endif
        if count(g:plug_groups, 'go')
            Plug 'zchee/deoplete-go', {'for': 'go'}
        endif
        if count(g:plug_groups, 'javascript')
            Plug 'carlitux/deoplete-ternjs'
        endif
    elseif g:complete_method == "completor"
        Plug 'maralla/completor.vim'
        if g:complete_snippet == "neosnippet"
            Plug 'maralla/completor-neosnippet'
        endif
    elseif g:complete_method == "neocomplete"
        Plug 'Shougo/neoinclude.vim'
        Plug 'Shougo/neocomplete.vim'
    elseif g:complete_method == "neocomplcache"
        Plug 'Shougo/neocomplcache.vim'
        if g:complete_snippet == 'ultisnips'
            Plug 'JazzCore/neocomplcache-ultisnips'
        endif
    elseif g:complete_method == "YCM"
        if g:python_version == 2
            Plug 'Valloric/YouCompleteMe',{ 'do' : 'python2 ./install.py --clang-completer' }
        elseif g:python_version == 3
            Plug 'Valloric/YouCompleteMe',{ 'do' : 'python3 ./install.py --clang-completer' }
        endif
    elseif g:complete_method == "ncm2"
        Plug 'ncm2/ncm2'
        Plug 'roxma/nvim-yarp'
        if !has('nvim')
            Plug 'roxma/vim-hug-neovim-rpc'
        endif
        " main complete
        Plug 'ncm2/ncm2-bufword'
        Plug 'ncm2/ncm2-path'
        Plug 'ncm2/ncm2-github'
        Plug 'ncm2/ncm2-tmux'
        Plug 'ncm2/ncm2-tagprefix'
        Plug 'filipekiss/ncm2-look.vim'
        Plug 'jsfaint/ncm2-vim'
        Plug 'Shougo/neco-vim'
        Plug 'Shougo/neoinclude.vim'
        Plug 'Shougo/neco-syntax'
        " only use ultisnips
        Plug 'ncm2/ncm2-ultisnips'
        " language
        Plug 'ncm2/ncm2-pyclang', {'for':['c', 'cpp']}
        if count(g:plug_groups, 'css')
            Plug 'ncm2/ncm2-cssomni', {'for':['css', 'css3']}
        endif
        if count(g:plug_groups, 'javascript')
            Plug 'ncm2/ncm2-tern', {'for':['javascript', 'typescript']}
        endif
        if count(g:plug_groups, 'python')
            Plug 'ncm2/ncm2-jedi', {'for':'python'}
        endif
        if count(g:plug_groups, 'rust')
            Plug 'ncm2/ncm2-racer', {'for':'rust'}
        endif
        if count(g:plug_groups, 'go')
            Plug 'ncm2/ncm2-go', {'for':'go'}
        endif
    endif
else
    echoe "In order to have grammar check and snippets completion, you should update to vim7.3+ or neovim!!"
endif
" after file
if filereadable(expand("~/.nvimrc.after")) && has('nvim')
    source ~/.nvimrc.after
elseif filereadable(expand("~/.gvimrc.after")) && has('gui_running')
    source ~/.gvimrc.after
elseif filereadable(expand("~/.vimrc.after"))
    source ~/.vimrc.after
endif
call plug#end()
function! s:PlugRenew()
    :PlugClean
    :PlugUpdate
endfunction
command! PlugRenew call s:PlugRenew()
